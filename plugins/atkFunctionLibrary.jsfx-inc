/*
Copyright the ATK Community and Joseph Anderson, Josh Parmenter, Trond Lossius, 2013

               J Anderson     j.anderson[at]ambisonictoolkit.net
               J Parmenter    j.parmenter[at]ambisonictoolkit.net
               T Lossius      trond.lossius[at]bek.no


This program is free software: you can redistribute it and/or modify
it under the terms of the GNU Lesser General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU Lesser General Public License for more details.

You should have received a copy of the GNU Lesser General Public License
and GNU General Public License along with this program.  If not, see
<http://www.gnu.org/licenses/>.


---------------------------------------------------------------------
  Class: atk-function-library.jsfx-inc
  Library of shared functions
---------------------------------------------------------------------



---------------------------------------------------------------------
Third Party Notices
---------------------------------------------------------------------


---------------------------------------------------------------------
The function for inverting a 4x4 matrix is lifted of StackOverflow:
http://stackoverflow.com/questions/1148309/inverting-a-4x4-matrix

The code there is itself lifted of the MESA implementation of the GLU library:
http://www.mesa3d.org/
---------------------------------------------------------------------



---------------------------------------------------------------------
  Class: atk-function-library.jsfx-inc
  Library of shared functions
---------------------------------------------------------------------
*/



/********************************************************************
Functions in the init section can be used by any plugin loading this
file, encouraging DRY (don't repeat yourself)
********************************************************************/
@init


/*---------------------------------------------------------------------
  Constants
---------------------------------------------------------------------*/
recSqrt2   = 1. / sqrt(2.);




/*---------------------------------------------------------------------
  Mathematical functions
---------------------------------------------------------------------*/

/********************************************************************
Maths: Convert linear gain to decibel
********************************************************************/
function db2amp(gain)
(
  pow(10.0, gain * 0.05);  // returns linear gain
);


/********************************************************************
Maths: Convert linear gain to decibel
********************************************************************/
function amp2db(gain)
(
  20 * log10(gain);       // returns dB gain
);



/*---------------------------------------------------------------------
  Matrix section
---------------------------------------------------------------------*/


/********************************************************************
Matrix: Generate 4x8 matrix with 8 source signals for display purposes
********************************************************************/
function generateDisplaySignalMatrix(matrix, numPoints) local(i, angle, inc)
(
  // Encoding equally distributed points in the horisontal plane
  i = 0;
  angle = 0.;
  inc = 2. * $pi / numPoints;
  loop(numPoints,
    matrix[i]                = recSqrt2;
    matrix[i + numPoints]    = cos(angle);
    matrix[i + 2*numPoints]  = sin(angle);
    matrix[i + 3*numPoints]  = 0.;
    
    angle += inc;
    i += 1;
  );
);


/********************************************************************
Matrix: Interprete processed points

In the resulting matrixOut:
- Row 1 is x position of transformed point
- Row 2 is y position of transformed point
- Row 3 is z position of transformed point
- Row 4 is gain value in dB for transformed point
********************************************************************/
function interpreteProcessedPoints(matrixOut, matrixIn, numPoints) local(i, g0square, g1square, w, x, y, z, azi, ele, omni, gain)
(
  i = 0;
  loop(numPoints,
    w = matrixIn[i];
    x = matrixIn[numPoints + i];
    y = matrixIn[numPoints*2 + i];
    z = matrixIn[numPoints*3 + i];
    
    azi  = atan2(y, x);
    ele  = atan2(z, x*x + y*y);
    
    // Omni transform angle
    g0square        = w * w;
    g1square        = x*x + y*y + z*z;
    omni = asin((2.*g0square - g1square) / (2.*g0square + g1square));
    
    // Gain
    gain = w * sqrt(2. / (1. + sin(omni)));
    
    // Normalise omni to [0,1] range
    omni = 2 * omni / $pi;
    omni = 1. - omni;
    
    // From this we calculate relative xyz position of the transformed point
    matrixOut[i]    = omni * cos(azi) * cos(ele);
    matrixOut[numPoints + i]  = omni * sin(azi) * cos(ele);
    matrixOut[numPoints*2 + i] = omni * sin(ele);
    matrixOut[numPoints*3 + i] = amp2db(gain);
    
    i += 1;
  );
);


/********************************************************************
Matrix: Generate 1st order 4x4 matrix for rotation transformation
********************************************************************/
function generateBtoAMatrix(matrix, orientation, weight) local(g0)
(
  // Option 0: orthogonal (front left up) [ FLU, FRD, BLD, BRU ]
  orientation == 0 ?
  ( 
    matrixDSP[0] =   0.5;
    matrixDSP[1] =   0.5;
    matrixDSP[2] =   0.5;
    matrixDSP[3] =   0.5;

    matrixDSP[4] =   0.5;
    matrixDSP[5] =   0.5;
    matrixDSP[6] =  -0.5;
    matrixDSP[7] =  -0.5;

    matrixDSP[8] =   0.5;
    matrixDSP[9] =  -0.5;
    matrixDSP[10] =  0.5;
    matrixDSP[11] = -0.5;

    matrixDSP[12] =  0.5;
    matrixDSP[13] = -0.5;
    matrixDSP[14] = -0.5;
    matrixDSP[15] =  0.5;
  ) :
  (
    // Option 1: front left down [ FLD, FRU, BLU, BRD ]
    orientation == 1 ?
    (
      matrixDSP[0] =  0.5;
      matrixDSP[1] =  0.5;
      matrixDSP[2] =  0.5;
      matrixDSP[3] = -0.5;

      matrixDSP[4] =  0.5;
      matrixDSP[5] =  0.5;
      matrixDSP[6] = -0.5;
      matrixDSP[7] =  0.5;

      matrixDSP[8] =  0.5;
      matrixDSP[9] = -0.5;
      matrixDSP[10] =  0.5;
      matrixDSP[11] =  0.5;

      matrixDSP[12] =  0.5;
      matrixDSP[13] = -0.5;
      matrixDSP[14] = -0.5;
      matrixDSP[15] = -0.5;
    ) :
    (
      // Option 2: front left-right [ FL, FR, BU, BD ]
    
      orientation == 2 ?
      (     
        matrixDSP[0] =  0.5;
        matrixDSP[1] =  0.5;
        matrixDSP[2] =  recSqrt2;
        matrixDSP[3] =  0.;

        matrixDSP[4] =  0.5;
        matrixDSP[5] =  0.5;
        matrixDSP[6] = -recSqrt2;
        matrixDSP[7] =  0.;

        matrixDSP[8] =  0.5;
        matrixDSP[9] = -0.5;
        matrixDSP[10] =  0.;
        matrixDSP[11] =  recSqrt2;

        matrixDSP[12] =  0.5;
        matrixDSP[13] = -0.5;
        matrixDSP[14] =  0.;
        matrixDSP[15] = -recSqrt2;
      ) :
      (
        // Option 3: front up-down [ FU, FD, BL, BR ]
      
        orientation == 3 ?
        (
          matrixDSP[0] =  0.5;
          matrixDSP[1] =  0.5;
          matrixDSP[2] =  0.;
          matrixDSP[3] =  recSqrt2;

          matrixDSP[4] =  0.5;
          matrixDSP[5] =  0.5;
          matrixDSP[6] =  0.;
          matrixDSP[7] = -recSqrt2;

          matrixDSP[8] =  0.5;
          matrixDSP[9] = -0.5;
          matrixDSP[10] =  recSqrt2;
          matrixDSP[11] =  0.;

          matrixDSP[12] =  0.5;
          matrixDSP[13] = -0.5;
          matrixDSP[14] = -recSqrt2;
          matrixDSP[15] =  0.;
        ) :
        (
          // Option 4: front & back down [ F, BD, BLU, BRU ]
        
          orientation == 4 ?
          (
            matrixDSP[0] =  0.5;
            matrixDSP[1] =  sqrt3Div2;
            matrixDSP[2] =  0.;
            matrixDSP[3] =  0.;

            matrixDSP[4] =  0.5;
            matrixDSP[5] = -sqrt3Div6;
            matrixDSP[6] =  0.;
            matrixDSP[7] = -sqrt6Div3;

            matrixDSP[8] =  0.5;
            matrixDSP[9] = -sqrt3Div6;
            matrixDSP[10] =  recSqrt2;
            matrixDSP[11] =  recSqrt6;

            matrixDSP[12] =  0.5;
            matrixDSP[13] = -sqrt3Div6;
            matrixDSP[14] = -recSqrt2;
            matrixDSP[15] =  recSqrt6;
          ) :
          (
            // Option 5: front & back up [ F, BU, BLD, BRD ]
          
            orientation == 5 ?
            (
              matrixDSP[0] =  0.5;
              matrixDSP[1] =  sqrt3Div2;
              matrixDSP[2] =  0.;
              matrixDSP[3] =  0.;

              matrixDSP[4] =  0.5;
              matrixDSP[5] = -sqrt3Div6;
              matrixDSP[6] =  0.0;
              matrixDSP[7] =  sqrt6Div3;

              matrixDSP[8] =  0.5;
              matrixDSP[9] = -sqrt3Div6;
              matrixDSP[10] =  recSqrt2;
              matrixDSP[11] = -recSqrt6;

              matrixDSP[12] =  0.5;
              matrixDSP[13] = -sqrt3Div6;
              matrixDSP[14] = -recSqrt2;
              matrixDSP[15] = -recSqrt6;
            ) :
            (
              // Option 6: front left-right up [ FLU, FRU, FD, B ]
            
              orientation == 6 ?
              (
                matrixDSP[0] =  0.5;
                matrixDSP[1] =  sqrt3Div6;
                matrixDSP[2] =  recSqrt2;
                matrixDSP[3] =  recSqrt6;

                matrixDSP[4] =  0.5;
                matrixDSP[5] =  sqrt3Div6;
                matrixDSP[6] = -recSqrt2;
                matrixDSP[7] =  recSqrt6;

                matrixDSP[8] =  0.5;
                matrixDSP[9] =  sqrt3Div6;
                matrixDSP[10] =  0.;
                matrixDSP[11] = -sqrt6Div3;

                matrixDSP[12] =  0.5;
                matrixDSP[13] = -sqrt3Div2;
                matrixDSP[14] =  0.;
                matrixDSP[15] =  0.;
              ) :
              (
                // Option 7: front left-right down [ FLD, FRD, FU, B ]

                matrixDSP[0] =  0.5;
                matrixDSP[1] =  sqrt3Div6;
                matrixDSP[2] =  recSqrt2;
                matrixDSP[3] = -recSqrt6;

                matrixDSP[4] =  0.5;
                matrixDSP[5] =  sqrt3Div6;
                matrixDSP[6] = -recSqrt2;
                matrixDSP[7] = -recSqrt6;

                matrixDSP[8] =  0.5;
                matrixDSP[9] =  sqrt3Div6;
                matrixDSP[10] =  0.;
                matrixDSP[11] =  sqrt6Div3;

                matrixDSP[12] =  0.5;
                matrixDSP[13] = -sqrt3Div2;
                matrixDSP[14] =  0.;
                matrixDSP[15] =  0.;
              );

            );

          );

        );

      );
    );
  );


  // Weight:
  // Option 0: Decorrelated (on the sphere)
  weight == 0 ?
  (
    g0 = sqrt(2./3.);
  ) :
  (
    // Option 1: Canonical
    weight == 1 ?
    (
      g0 = 1.;
    ) :
    (
      // Option 2: Unscaled, W_gain = 1
      weight == 2 ?
      (
        g0 = sqrt(2.);
      ) :
      // Option 3: Cardioid
      (
        g0 = sqrt(6.);
      );
    );
  );

  // Compensate matrix for weight:
  matrixDSP[0] =  g0 * matrixDSP[0];
  matrixDSP[4] =  g0 * matrixDSP[4];
  matrixDSP[8] =  g0 * matrixDSP[8];
  matrixDSP[12] = g0 * matrixDSP[12];
);


/********************************************************************
Matrix: Generate 1st order 4x4 matrix for directX transformation
********************************************************************/
function generateDirectMatrix(matrix, angle) local(g0, g1)
(
	g0 = sqrt(1 + sin(angle));
	g1 = sqrt(1 - sin(angle));
  
  matrix[0]  = g0;
  matrix[1]  = 0.;
  matrix[2]  = 0.;
  matrix[3]  = 0.;
  
  matrix[4]  = 0.;
  matrix[5]  = g1;
  matrix[6]  = 0.;
  matrix[7]  = 0.;
  
  matrix[8]  = 0.;
  matrix[9]  = 0.;
  matrix[10] = g0;
  matrix[11] = 0.;
  
  matrix[12] = 0.;
  matrix[13] = 0.;
  matrix[14] = 0.;
  matrix[15] = g0;
);


/********************************************************************
Matrix: Generate 1st order 4x4 matrix for directX transformation
********************************************************************/
function generateDirectOrigoMatrix(matrix, angle) local(g0, g1)
(
  
  g0 = sqrt(1 + sin(transformAngle));
  g1 = sqrt(1 - sin(transformAngle));
  
  matrix[0]  = g0;
  matrix[1]  = 0.;
  matrix[2]  = 0.;
  matrix[3]  = 0.;
  
  matrix[4]  = 0.;
  matrix[5]  = g1;
  matrix[6]  = 0.;
  matrix[7]  = 0.;
  
  matrix[8]  = 0.;
  matrix[9]  = 0.;
  matrix[10] = g1;
  matrix[11] = 0.;
  
  matrix[12] = 0.;
  matrix[13] = 0.;
  matrix[14] = 0.;
  matrix[15] = g1;
);

/********************************************************************
Matrix: Generate 1st order 4x4 matrix for dominateX transformation
********************************************************************/
function generateDominateMatrix(matrix, gain) local(k, g0, g1)
(
	k = db2amp(gain);		
	g0 = (k + 1. / k) * 0.5;
	g1 = (k - 1. / k) / sqrt(2.);
    
  matrix[0]  = g0;
  matrix[1]  = g1 * 0.5;
  matrix[2]  = 0.;
  matrix[3]  = 0.;
  
  matrix[4]  = g1;
  matrix[5]  = g0;
  matrix[6]  = 0.;
  matrix[7]  = 0.;
  
  matrix[8]  = 0.;
  matrix[9]  = 0.;
  matrix[10] = 1.;
  matrix[11] = 0.;
  
  matrix[12] = 0.;
  matrix[13] = 0.;
  matrix[14] = 0.;
  matrix[15] = 1.;
);


/********************************************************************
Matrix: Generate 1st order 4x4 matrix for focus transformation
********************************************************************/
function generateFocusMatrix(matrix, angle) local(g0, g1, g2)
(  
	g0 = 1. / (1 + sin(abs(angle)));
	g1 = sqrt(2) *  sin(angle) * g0;
	g2 = cos(angle) * g0;
  
  matrix[0]  = g0;
  matrix[1]  = g1 * 0.5;
  matrix[2]  = 0.;
  matrix[3]  = 0.;

  matrix[4]  = g1; 
  matrix[5]  = g0;
  matrix[6]  = 0.;
  matrix[7]  = 0.;
  
  matrix[8]  = 0.;
  matrix[9]  = 0.;
  matrix[10] = g2;
  matrix[11] = 0.;
  
  matrix[12] = 0.;
  matrix[13] = 0.;
  matrix[14] = 0.;
  matrix[15] = g2;
);


/********************************************************************
Matrix: Generate 1st order 4x4 matrix for pressX transformation
********************************************************************/
function generatePressMatrix(matrix, angle) local(g0, g1, g2)
(
	g0 = sqrt(2.) * sin(angle) * sin(abs(angle));
  g2 = cos(angle);
  g1 = g2 * g2;
  
  matrix[0]  = 1.;
  matrix[1]  = 0.;
  matrix[2]  = 0.;
  matrix[3]  = 0.;
  
  matrix[4]  = g0;
  matrix[5]  = g1;
  matrix[6]  = 0.;
  matrix[7]  = 0.;
  
  matrix[8]  = 0.;
  matrix[9]  = 0.;
  matrix[10] = g2;
  matrix[11] = 0.;
  
  matrix[12] = 0.;
  matrix[13] = 0.;
  matrix[14] = 0.;
  matrix[15] = g2;
);


/********************************************************************
Matrix: Generate 1st order 4x4 matrix for pushX transformation
********************************************************************/
function generatePushMatrix(matrix, angle) local(g0, g1)
(  
  g0 = sqrt(2.) * sin(angle) * sin(abs(angle));
  g1 = cos(angle);
  g1 = g1 * g1;
    
  matrix[0]  = 1.;
  matrix[1]  = 0.;
  matrix[2]  = 0.;
  matrix[3]  = 0.;
  
  matrix[4]  = g0;
  matrix[5]  = g1;
  matrix[6]  = 0.;
  matrix[7]  = 0.;
  
  matrix[8]  = 0.;
  matrix[9]  = 0.;
  matrix[10] = g1;
  matrix[11] = 0.;
  
  matrix[12] = 0.;
  matrix[13] = 0.;
  matrix[14] = 0.;
  matrix[15] = g1;
);


/********************************************************************
Matrix: Generate 1st order 4x4 matrix for rotation transformation
********************************************************************/
function generateRotationMatrix(matrix, angle)
(
  // We use SpatDIF (navigational) coordinate system rather than mathematical.
  
  matrix[0]  = 1.;
  matrix[1]  = 0.;
  matrix[2]  = 0.;
  matrix[3]  = 0.;
  
  matrix[4]  = 0.;
  matrix[5]  = cos(angle);
  matrix[6]  = sin(angle);
  matrix[7]  = 0.;
  
  matrix[8]  = 0.;
  matrix[9]  = -matrix[6];
  matrix[10] =  matrix[5];
  matrix[11] = 0.;
  
  matrix[12] = 0.;
  matrix[13] = 0.;
  matrix[14] = 0.;
  matrix[15] = 1.;
);



/********************************************************************
Matrix: Generate 1st order 4x4 matrix for tilt transformation
********************************************************************/
function generateTiltlMatrix(matrix, angle)
(  
  matrix[0]  =  1.;
  matrix[1]  =  0.;
  matrix[2]  =  0.;
  matrix[3]  =  0.;
  
  matrix[4]  =  0.;
  matrix[5]  =  1.;
  matrix[6]  =  0.;
  matrix[7]  =  0.;
  
  matrix[8]  =  0.;
  matrix[9]  =  0.;
  matrix[10] =  cos(angle);
  matrix[11] = -sin(angle);
  
  matrix[12] =  0.;
  matrix[13] =  0.;
  matrix[14] = -matrix[11];
  matrix[15] =  matrix[10];  
);




/********************************************************************
Matrix: Generate 1st order 4x4 matrix for tumble transformation
********************************************************************/
function generateTumblelMatrix(matrix, angle)
(  
  matrix[0]  =  1.;
  matrix[1]  =  0.;
  matrix[2]  =  0.;
  matrix[3]  =  0.;
  
  matrix[4]  =  0.;
  matrix[5]  =  cos(angle);
  matrix[6]  =  0.;
  matrix[7]  = -sin(angle);
  
  matrix[8]  =  0.;
  matrix[9]  =  0.;
  matrix[10] =  1.;
  matrix[11] =  0.;
  
  matrix[12] =  0.;
  matrix[13] = -matrix[7];
  matrix[14] =  0.;
  matrix[15] =  matrix[5];  
);


/********************************************************************
Matrix: Generate 1st order 4x4 matrix for zoomX transformation
********************************************************************/
function generateZoomMatrix(matrix, angle) local(g0, g1)
(
  g0 = sin(angle);
  g1 = cos(angle);
    
  matrix[0]  = 1.;
  matrix[1]  = g0 / sqrt(2.);
  matrix[2]  = 0.;
  matrix[3]  = 0.;
  
  matrix[4]  = g0 * sqrt(2.);
  matrix[5]  = 1.;
  matrix[6]  = 0.;
  matrix[7]  = 0.;
  
  matrix[8]  = 0.;
  matrix[9]  = 0.;
  matrix[10] = g1;
  matrix[11] = 0.;
  
  matrix[12] = 0.;
  matrix[13] = 0.;
  matrix[14] = 0.;
  matrix[15] = g1;
);


/********************************************************************
Matrix: Multiplication of two 4x4 matrixes: result = left x right
********************************************************************/
function matrixMultiplication(resultMatrix, numRowsLeft, numColsLeft, leftMatrix, numRowsRight, numColsRight, rightMatrix) local(row, col, i)
(
  // It is the responsibility of each plugin calling this function to ensure that numColsLeft == numRowsRight.
  // We do not check for that here.
  
  // Iterate over rows of left matrix
  row = 0;
  loop(numRowsLeft, 
    // Iterate over columns of right matrix
    col = 0;
    loop(numColsRight,
      // Calculate one cell
      resultMatrix[numColsRight*row + col] = 0.;
      i = 0;
      loop(numColsLeft,
        resultMatrix[numColsRight*row + col] += leftMatrix[numColsLeft*row + i] * rightMatrix[numColsRight*i + col];
        i += 1;
      );
      col += 1;
    );
    row += 1;
  );
);


/********************************************************************
Matrix: Invert a 4x4 matrix
********************************************************************/
function invertMatrix(matrix)
(
  inv[0] =   matrix[5]  * matrix[10] * matrix[15] -
             matrix[5]  * matrix[11] * matrix[14] -
             matrix[9]  * matrix[6]  * matrix[15] +
             matrix[9]  * matrix[7]  * matrix[14] +
             matrix[13] * matrix[6]  * matrix[11] -
             matrix[13] * matrix[7]  * matrix[10];

  inv[4] =  -matrix[4]  * matrix[10]  * matrix[15] +
             matrix[4]  * matrix[11]  * matrix[14] +
             matrix[8]  * matrix[6]  * matrix[15] -
             matrix[8]  * matrix[7]  * matrix[14] -
             matrix[12] * matrix[6]  * matrix[11] +
             matrix[12] * matrix[7]  * matrix[10];

  inv[8] =   matrix[4]  * matrix[9]  * matrix[15] -
             matrix[4]  * matrix[11] * matrix[13] -
             matrix[8]  * matrix[5]  * matrix[15] +
             matrix[8]  * matrix[7]  * matrix[13] +
             matrix[12] * matrix[5]  * matrix[11] -
             matrix[12] * matrix[7]  * matrix[9];

  inv[12] = -matrix[4]  * matrix[9]  * matrix[14] +
             matrix[4]  * matrix[10] * matrix[13] +
             matrix[8]  * matrix[5]  * matrix[14] -
             matrix[8]  * matrix[6]  * matrix[13] -
             matrix[12] * matrix[5]  * matrix[10] +
             matrix[12] * matrix[6]  * matrix[9];

  inv[1] =  -matrix[1]  * matrix[10] * matrix[15] +
             matrix[1]  * matrix[11] * matrix[14] +
             matrix[9]  * matrix[2]  * matrix[15] -
             matrix[9]  * matrix[3]  * matrix[14] -
             matrix[13] * matrix[2]  * matrix[11] +
             matrix[13] * matrix[3]  * matrix[10];

  inv[5] =   matrix[0]  * matrix[10] * matrix[15] -
             matrix[0]  * matrix[11] * matrix[14] -
             matrix[8]  * matrix[2]  * matrix[15] +
             matrix[8]  * matrix[3]  * matrix[14] +
             matrix[12] * matrix[2]  * matrix[11] - 
             matrix[12] * matrix[3]  * matrix[10];

  inv[9] =  -matrix[0]  * matrix[9]  * matrix[15] +
             matrix[0]  * matrix[11] * matrix[13] +
             matrix[8]  * matrix[1]  * matrix[15] -
             matrix[8]  * matrix[3]  * matrix[13] -
             matrix[12] * matrix[1]  * matrix[11] +
             matrix[12] * matrix[3]  * matrix[9];

  inv[13] =  matrix[0]  * matrix[9]  * matrix[14] -
             matrix[0]  * matrix[10] * matrix[13] -
             matrix[8]  * matrix[1]  * matrix[14] +
             matrix[8]  * matrix[2]  * matrix[13] +
             matrix[12] * matrix[1]  * matrix[10] -
             matrix[12] * matrix[2]  * matrix[9];

  inv[2] =   matrix[1]  * matrix[6]  * matrix[15] -
             matrix[1]  * matrix[7]  * matrix[14] -
             matrix[5]  * matrix[2]  * matrix[15] +
             matrix[5]  * matrix[3]  * matrix[14] +
             matrix[13] * matrix[2]  * matrix[7] -
             matrix[13] * matrix[3]  * matrix[6];

  inv[6] =  -matrix[0]  * matrix[6]  * matrix[15] +
             matrix[0]  * matrix[7]  * matrix[14] +
             matrix[4]  * matrix[2]  * matrix[15] -
             matrix[4]  * matrix[3]  * matrix[14] -
             matrix[12] * matrix[2]  * matrix[7] +
             matrix[12] * matrix[3]  * matrix[6];

  inv[10] =  matrix[0]  * matrix[5]  * matrix[15] -
             matrix[0]  * matrix[7]  * matrix[13] -
             matrix[4]  * matrix[1]  * matrix[15] +
             matrix[4]  * matrix[3]  * matrix[13] +
             matrix[12] * matrix[1]  * matrix[7] -
             matrix[12] * matrix[3]  * matrix[5];

  inv[14] = -matrix[0]  * matrix[5]  * matrix[14] +
             matrix[0]  * matrix[6]  * matrix[13] +
             matrix[4]  * matrix[1]  * matrix[14] -
             matrix[4]  * matrix[2]  * matrix[13] -
             matrix[12] * matrix[1]  * matrix[6] +
             matrix[12] * matrix[2]  * matrix[5];

  inv[3] =  -matrix[1]  * matrix[6]  * matrix[11] +
             matrix[1]  * matrix[7]  * matrix[10] +
             matrix[5]  * matrix[2]  * matrix[11] -
             matrix[5]  * matrix[3]  * matrix[10] -
             matrix[9]  * matrix[2]  * matrix[7] +
             matrix[9]  * matrix[3]  * matrix[6];

  inv[7] =   matrix[0]  * matrix[6]  * matrix[11] -
             matrix[0]  * matrix[7]  * matrix[10] -
             matrix[4]  * matrix[2]  * matrix[11] +
             matrix[4]  * matrix[3]  * matrix[10] +
             matrix[8]  * matrix[2]  * matrix[7] -
             matrix[8]  * matrix[3]  * matrix[6];

  inv[11] = -matrix[0]  * matrix[5]  * matrix[11] +
             matrix[0]  * matrix[7]  * matrix[9] +
             matrix[4]  * matrix[1]  * matrix[11] -
             matrix[4]  * matrix[3]  * matrix[9] -
             matrix[8]  * matrix[1]  * matrix[7] +
             matrix[8]  * matrix[3]  * matrix[5];

  inv[15] =  matrix[0]  * matrix[5]  * matrix[10] -
             matrix[0]  * matrix[6]  * matrix[9] -
             matrix[4]  * matrix[1]  * matrix[10] +
             matrix[4]  * matrix[2]  * matrix[9] +
             matrix[8]  * matrix[1]  * matrix[6] -
             matrix[8]  * matrix[2]  * matrix[5];
					
  determinant = matrix[0] * inv[0] +
	              matrix[1] * inv[4] +
				        matrix[2] * inv[8] +
				        matrix[3] * inv[12];

  /* Might be difficult to check this in Reaper/JS, but it might also be redundant?
	if (determinant == 0)
    return false;
	*/

  determinant = 1.0 / determinant;

  matrix[0]  = inv[0]  * determinant;
	matrix[1]  = inv[1]  * determinant;
	matrix[2]  = inv[2]  * determinant;
	matrix[3]  = inv[3]  * determinant;
	
	matrix[4]  = inv[4]  * determinant;
	matrix[5]  = inv[5]  * determinant;
	matrix[6]  = inv[6]  * determinant;
	matrix[7]  = inv[7]  * determinant;
	
	matrix[8]  = inv[8]  * determinant;
	matrix[9]  = inv[9]  * determinant;
	matrix[10] = inv[10] * determinant;
	matrix[11] = inv[11] * determinant;
	
	matrix[12] = inv[12] * determinant;
	matrix[13] = inv[13] * determinant;
	matrix[14] = inv[14] * determinant;
	matrix[15] = inv[15] * determinant;
);





/*---------------------------------------------------------------------
  Graphics section
---------------------------------------------------------------------*/

/********************************************************************
Graphics: Set drawing color
********************************************************************/
function gxColorSet(alpha, red, green, blue)
(
	gfx_a = alpha;
	gfx_r = red;
	gfx_g = green;
	gfx_b = blue;
);


/********************************************************************
Graphics: Set drawing position
********************************************************************/
function gxPositionSet(x, y)
(
	gfx_x = x;
	gfx_y = y;
);


/********************************************************************
Graphics: Color convertion utility for hsl to rgb convertions
********************************************************************/
function hls_value(n1, n2, hue)
(
	// Restrict incoming range to [0., 360.]
  hue > 360.0 ? (
    hue -= 360.;
  ) : (
    hue < 0.0 ? (
      hue += 360.;
    );
  );
  
  hue < 60. ? (
    n1+(n2-n1)*hue/60.
  ) : (
    hue < 180. ? (
      n2;
    ) : (
      hue < 240. ? (
        n1+(n2-n1)*(240-hue)/60.;
      ) : (
        n1;
      );
    );
  );
);


/********************************************************************
Graphics: Express gain (in dB) as graphics color
********************************************************************/
function gainToColor(gain, gainLowLimit, gainHighLimit, verticalOffset) local(saturation, lightness, m1, m2, red, gree, blue)
(
  // Clip gain to prescribed range:
  gain < gainLowLimit ? (
    gain = gainLowLimit;
  ) : (
    gain > gainHighLimit ? (
      gain = gainHighLimit;
    );
  );
  
  hue = ((gain - gainHighLimit) / (gainLowLimit - gainHighLimit)) * 240.;
  
  // Saturation and lightness depends on vertical position
  verticalOffset < 0 ? (
    saturation = 1. - 0.3*verticalOffset;
    lightness = 0.5;
  ) : (
    saturation = 1.;
    lightness = 0.5 + 0.3*verticalOffset;
  );
  
  lightness <= 0.5 ? (
    m2 = lightness*(1.0+saturation);
  ) : (
    m2 = lightness+saturation-lightness*saturation;
  );
  
  m1 = 2.0 * lightness-m2;
  
  saturation == 0.0 ? (
    red = lightness;
  	green = lightness;
  	blue = lightness;
  ) : (
    red = hls_value(m1, m2, hue+120.0);
  	green = hls_value(m1, m2, hue);
  	blue = hls_value(m1, m2, hue-120.0);
  );

  gxColorSet(1., red, green, blue)
);


/********************************************************************
Graphics: Determine geometry
********************************************************************/
function determineGeometry()
(
  centerX = gfx_w  * 0.5;
  centerY = gfx_h * 0.5;

  centerX < centerY ? radius = centerX : radius = centerY;
  radius = radius - 10;
  inverseRadius = 1. / radius;
);


/********************************************************************
Graphics: Draw background image based on ICST ambimonitor
********************************************************************/
function drawBackgroundImage(sourceWidth, sourceHeight, dstLeft, dstTop, dstWidth, dstHeight)
(
  // Background graphics
  coordinatelist[0] = 0;                  //left-most pixel location for source
  coordinatelist[1] = 0;                  //top-most pixel location for source
  coordinatelist[2] = sourceWidth;        //width of source
  coordinatelist[3] = sourceHeight;       //height of source
  
  //coordinatelist[4] = centerX - radius;   //left-most pixel location of destination
  //coordinatelist[5] = centerY - radius;   //top-most pixel location of destination
  //coordinatelist[6] = 2 * radius;         //width of destination to fill
  //coordinatelist[7] = 2 * radius;         //height of destination to fill
  
  coordinatelist[4] = dstLeft;            //left-most pixel location of destination
  coordinatelist[5] = dstTop;             //top-most pixel location of destination
  coordinatelist[6] = dstWidth;           //width of destination to fill
  coordinatelist[7] = dstHeight;          //height of destination to fill
  coordinatelist[8] =0;                   //represents offset from center of image
  coordinatelist[9] =0;                   //represents offset from center of image
  gfx_blitext(0,coordinatelist,0);

);



/********************************************************************
Graphics: Draw line segment
********************************************************************/
function drawLineSegment(position1x, position1y, position2x, position2y, antiAliasing)
(
  gfx_x = position1x;
  gfx_y = position1y;
  gfx_lineto(position2x, position2y, antiAliasing);
);


/********************************************************************
Graphics: Draw outline of polygon with four sides and four corners
********************************************************************/
function drawQuadrangleOutline(position1x, position1y, position2x, position2y, position3x, position3y, position4x, position4y, antiAliasing)
(
  gfx_x = position1x;
  gfx_y = position1y;
  gfx_lineto(position2x, position2y, antiAliasing);
  gfx_lineto(position3x, position3y, antiAliasing);
  gfx_lineto(position4x, position4y, antiAliasing);
  gfx_lineto(position1x, position1y, antiAliasing);
);


/********************************************************************
Graphics: Draw centered filled-in square
********************************************************************/
function drawCenteredSquare(centerX, centerY, squareSize) local(halfSize)
(
  halfSize = squareSize * 0.5;
  gfx_x = centerX - halfSize;
  gfx_y = centerY - halfSize;
  gfx_rectto(centerX + halfSize, centerY + halfSize);

);


/********************************************************************
Graphics: Draw interactive square
********************************************************************/
function drawInteractionSquare(centerX, centerY, squareSize) local(halfSize, a, r, g, b)
(
  // Temporarily store incoming color
	a = gfx_a;
	r = gfx_r;
	g = gfx_g;
	b = gfx_b;
  
  // Draw square with white frame:
  drawCenteredSquare(centerX, centerY, squareSize);
  gxColorSet(1., 1., 1., 1.);
  halfSize = squareSize * 0.5;
  drawQuadrangleOutline(centerX-halfSize, centerY-halfSize, 
                        centerX+halfSize, centerY-halfSize,
                        centerX+halfSize, centerY+halfSize,
                        centerX-halfSize, centerY+halfSize,
                        1.
                      );
                      
  // Return color to its prior value
  gxColorSet(a, r, g, b);
);


/********************************************************************
Graphics: Draw points illustrating the effect of various transforms
********************************************************************/
function displayTransformedPoints(matrix, numPoints) local(i, posX, posY, posZ, squareSize)
(
  // Draw eight points indicating the effect of the transform
  // TODO: Also take z value into account in this visualisation?
  i = 0;
  loop(
    numPoints,
    posX = centerX - radius * matrix[numPoints+i];
    posY = centerY - radius * matrix[i];
    posZ = matrix[2*numPoints+i];
    
    // Size, saturation and luminocity depends on posZ
    squareSize = 2. * posZ + 8.;
    gainToColor(matrix[3*numPoints+i], -24., 6., posZ);
    
    drawCenteredSquare(posX, posY, squareSize);
    i += 1;
  );
);